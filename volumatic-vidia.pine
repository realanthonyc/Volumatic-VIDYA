// This Pine Script® code is subject to the terms of the Mozilla Public License 2.0 at https://mozilla.org/MPL/2.0/
// © realanthonyc https://www.tradingview.com/u/realanthonyc

//@version=6
// ------------------------------------------------------------------
//  Volumatic VIDYA (Variable Index Dynamic Average) Optimized
//  On Github: https://github.com/realanthonyc/Volumatic-VIDYA
//  Forked from "Variable Index Dynamic Average" by BigBeluga
//  v1.6.9
// ------------------------------------------------------------------
indicator(title="Volumatic VIDYA Optimized ", shorttitle="Vol-VIDYA", overlay=true, max_lines_count=500, max_labels_count=500)

// === INPUTS ===

// Regular configuration for daily and higher timeframes
int vidya_length = input.int(10, 'VIDYA Length', group = 'Regular Configuration') // Length of the VIDYA calculation
int vidya_momentum = input.int(20, 'VIDYA Momentum', group = 'Regular Configuration') // Momentum length for VIDYA
float band_distance = input.float(2.00, 'Distance for VIDYA Lines', step = 0.05, group = 'Regular Configuration') // Multiplier for ATR to set band distance

// Sensitive configuration for lower timeframes
int vidya_length_s = input.int(3, 'VIDYA Length', group = 'Sensitive Configuration')
int vidya_momentum_s = input.int(9, 'VIDYA Momentum', group = 'Sensitive Configuration')
float band_distance_s = input.float(1.25, 'Distance for VIDYA Lines', step = 0.05, group = 'Sensitive Configuration')

// General settings
float source = input.source(close, 'Source', group = 'General Settings')
// Define pivot parameters
int pivot_bars = input.int(3, 'Pivot Bars', group = 'General Settings')
int pivot_left_bars = pivot_bars
int pivot_right_bars = pivot_bars
// Optimization: Max number of liquidity lines to keep (prevents memory bloat)
int max_liquidity_lines = input.int(30, 'Max Liquidity Lines', minval=5, maxval=200, group = 'General Settings')
// KDJ sideways detection
float kdj_k_thresh = input.float(13, 'Sideway Threshold (K)', step=1, group='General Settings')

// Colors
color up_trend_color = input.color(#17dfad, 'Uptrend', group = 'Colors', inline = 'c')
color down_trend_color = input.color(#dd326b, 'Downtrend', group = 'Colors', inline = 'c')
bool shadow = input.bool(true, 'Enable Shadow', group = 'Colors')
int shadow_transparency = input.int(82, 'Shadow Transparency', minval = 0, maxval = 100, group = 'Colors')
color fill_gradient_end_color = input.color(color.rgb(255, 255, 255, 100), 'Fill Gradient End Color', group = 'Colors')
color volume_label_bg_color = input.color(color.rgb(255, 255, 255, 60), 'Volume Label Background', group = 'Colors')

// === INITIALIZATION ===

// Initialize variables for line, volume, and trend state
var line pivot_line = na // Variable for storing line references
var float volume_value = na // Variable for storing volume data
float smoothed_value = na // Smoothing variable for VIDYA trend levels
var bool is_trend_up = false // Boolean variable for tracking trend direction

// Initialize arrays for storing line and volume information
var array<line> liquidity_lines_low = array.new<line>() // Array for storing lines for lows
var array<line> liquidity_lines_high = array.new<line>() // Array for storing lines for highs

// Initialize volume counters to 0.0 instead of na to prevent na propagation
var float up_trend_volume = 0.0
var float down_trend_volume = 0.0

// === FUNCTIONS ===

// Define VIDYA
vidya_calc(src, vidya_length, vidya_momentum) =>
    float momentum = ta.change(src)
    float sum_pos_momentum = math.sum(momentum >= 0 ? momentum : 0.0, vidya_momentum)
    float sum_neg_momentum = math.sum(momentum >= 0 ? 0.0 : -momentum, vidya_momentum)
    float abs_cmo = math.abs(100 * (sum_pos_momentum - sum_neg_momentum) / (sum_pos_momentum + sum_neg_momentum))
    float alpha = 2 / (vidya_length + 1)
    var float vidya_value = 0.0
    vidya_value := alpha * abs_cmo / 100 * src + (1 - alpha * abs_cmo / 100) * nz(vidya_value[1])
    ta.sma(vidya_value, 15)

// Method to safely push lines and limit array size
method safe_push(array<line> line_array, line new_line) =>
    line_array.push(new_line)
    if line_array.size() > max_liquidity_lines
        line.delete(line_array.shift())

// Method to extend lines and add labels for liquidity levels (single 'if' for loop)
method extend_liquidity_lines(array<line> line_array, float price_level, bool is_cross, volume_val) =>
    if line_array.size() > 0 and last_bar_index - bar_index < 5000
        int i = line_array.size() - 1
        while i >= 0
            line liquidity_line = line_array.get(i)
            float current_line_level = line.get_y2(liquidity_line)
            bool price_cross = is_cross ? price_level < current_line_level and price_level[1] >= current_line_level : price_level > current_line_level and price_level[1] <= current_line_level
            bool is_short_line = bar_index - line.get_x1(liquidity_line) < 50
            if price_cross and is_short_line
                line.set_x2(liquidity_line, bar_index)
                line_array.remove(i)
                // Add volume label to the liquidity zone
                label.new(bar_index - 1, price_level[1], str.tostring(volume_val, format.volume), color = volume_label_bg_color, style = is_cross ? label.style_label_lower_left : label.style_label_upper_left, textcolor = chart.fg_color, size = size.small)
                // Add a circle label to represent liquidity zone
                label.new(bar_index - 1, price_level[1], text = '◉', color = color.rgb(0, 0, 0, 100), textcolor = is_cross ? down_trend_color : up_trend_color, style = label.style_label_center, size = size.normal)
            i -= 1

// === CALCULATIONS ===

// Calculate the Average True Range (ATR)
float atr_value = ta.atr(200)

// Switch to sensitive settings for not daily or higher timeframes
bool is_daily_or_higher = timeframe.isdaily or timeframe.isweekly or timeframe.ismonthly
bool use_sensitive_config = not is_daily_or_higher
int vidya_length_active = use_sensitive_config ? vidya_length_s : vidya_length
int vidya_momentum_active = use_sensitive_config ? vidya_momentum_s : vidya_momentum
float band_distance_active = use_sensitive_config ? band_distance_s : band_distance

// Calculate the VIDYA
float vidya_value = vidya_calc(source, vidya_length_active, vidya_momentum_active)

// Calculate upper and lower bands based on VIDYA and ATR
float upper_band = vidya_value + atr_value * band_distance_active
float lower_band = vidya_value - atr_value * band_distance_active

// Detect trend direction using crossovers of source with bands
if ta.crossover(source, upper_band)
    is_trend_up := true
if ta.crossunder(source, lower_band)
    is_trend_up := false

// Set trend-based smoothing variable
smoothed_value := is_trend_up ? lower_band : upper_band
smoothed_value := ta.change(is_trend_up) ? na : smoothed_value

// Calculate pivot highs and lows for price action
bool pivot_high = not na(ta.pivothigh(pivot_left_bars, pivot_right_bars))
bool pivot_low = not na(ta.pivotlow(close, pivot_left_bars, pivot_right_bars))

// Create and store lines for pivot lows (support zones)
if low[pivot_right_bars] > smoothed_value and pivot_low
    pivot_line := line.new(bar_index[pivot_right_bars], low[pivot_right_bars], bar_index[pivot_right_bars] + 5, low[pivot_right_bars], color = color.new(up_trend_color, 60))
    line.set_style(pivot_line, line.style_dotted)
    liquidity_lines_low.safe_push(pivot_line)
    // Use 0 if volume is na
    volume_value := na(volume) ? 0.0 : math.sum(volume, pivot_right_bars + pivot_left_bars) / (pivot_right_bars + pivot_left_bars)

// Create and store lines for pivot highs (resistance zones)
if high[pivot_right_bars] < smoothed_value and pivot_high
    pivot_line := line.new(bar_index[pivot_right_bars], high[pivot_right_bars], bar_index[pivot_right_bars] + 5, high[pivot_right_bars], color = color.new(down_trend_color, 60))
    line.set_style(pivot_line, line.style_dotted)
    liquidity_lines_high.safe_push(pivot_line)
    // Use 0 if volume is na
    volume_value := na(volume) ? 0.0 : math.sum(-volume, pivot_right_bars + pivot_left_bars) / (pivot_right_bars + pivot_left_bars)

// Extend lines to track price movements
liquidity_lines_high.extend_liquidity_lines(smoothed_value, true, volume_value)
liquidity_lines_low.extend_liquidity_lines(smoothed_value, false, volume_value)

// Detect and reset volume counters when trend changes, else accumulate volume
bool trend_changed = ta.change(is_trend_up)
if trend_changed
    up_trend_volume := 0.0
    down_trend_volume := 0.0
else if not na(volume)
    up_trend_volume += close > open ? volume : 0
    down_trend_volume += close < open ? volume : 0

// Calculate average volume
float avg_volume_delta = (up_trend_volume + down_trend_volume) / 2

// Determine the color of the trend
color trend_color = is_trend_up ? up_trend_color : down_trend_color

// Calculation of delta_volume — always show '0%' if avg is 0 or na
float volume_diff = up_trend_volume - down_trend_volume
string delta_volume = (na(avg_volume_delta) or avg_volume_delta == 0) ? '0%' : str.tostring((volume_diff / avg_volume_delta) * 100, format.percent)

// === KDJ SIDEWAYS DETECTION ===
float k_value = ta.stoch(close, high, low, 9) // KDJ length value
k_value := ta.sma(k_value, 3) // Smooth K value
bool k_in_middle = k_value >= 30 and k_value <= 70
float k_change = math.abs(k_value - k_value[1])
bool k_stable = k_change <= kdj_k_thresh
bool is_sideways = k_in_middle and k_stable and not na(k_value[1])
int fill_transparency = is_sideways ? 92 : (shadow ? shadow_transparency : 100)

// === PLOT ===

// Display labels for volume and trend statistics on the last bar
if barstate.islast
    label.delete(label.new(bar_index, smoothed_value, 'Buy: ' + str.tostring(up_trend_volume, format.volume) + '\n Sell: ' + str.tostring(down_trend_volume, format.volume) + '\n Delta: ' + delta_volume, color = color.new(trend_color, 90), style = is_trend_up ? label.style_label_upper_left : label.style_label_lower_left, textcolor = chart.fg_color, size = size.small)[1])
    label.delete(label.new(bar_index, smoothed_value, text = '●', color = color.rgb(0, 0, 0, 100), textcolor = trend_color, style = label.style_label_center, size = size.small)[1])

// Plot the VIDYA trend line
p1 = plot(smoothed_value, color = trend_color, linewidth = 2, style = plot.style_linebr, title = 'VIDYA Trend Lines')
p2 = plot(hl2, display = display.none, title = 'HL2 Reference')

// Fill between VIDYA trend line and HL2 with gradient fill
fill(p1, p2, smoothed_value, hl2, color.new(trend_color, fill_transparency), fill_gradient_end_color)

// === ALERTS ===
alertcondition(ta.change(is_trend_up) and is_trend_up, 'Trend Up', 'Trend has changed to Up')
alertcondition(ta.change(is_trend_up) and not is_trend_up, 'Trend Down', 'Trend has changed to Down')
alertcondition(ta.change(is_trend_up), 'Trend Changed', 'Trend has changed direction')